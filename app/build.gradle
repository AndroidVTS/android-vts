apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.nowsecure.android.vts"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 13
        versionName "v.13"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
        }
    }

    sourceSets.main {
        jni.srcDirs = [] // This prevents the auto generation of Android.mk
        // This tells gradle to include the objects in libs/ which were generated by running our
        // Android.mk basically by shelling out
        jniLibs.srcDir 'src/main/libs'
    }
}

android {
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.github.paolorotolo:appintro:3.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta3'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta3'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta3'
    compile 'commons-io:commons-io:2.4'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'org.apache.commons:commons-compress:1.5'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.3@aar') {
        transitive = true
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
ext.ndkDir = properties.getProperty('ndk.dir')

import org.apache.tools.ant.taskdefs.condition.Os
def getNDKCommand() {
    if (ext.ndkDir == null) {
        ext.ndkDir = file(System.getenv('NDK_ROOT'))
    }

    if (ext.ndkDir == null) {
        def gradle_project_root = project.rootProject.rootDir
        throw new GradleException("NDK is not configured. Make sure there is a local.properties " +
                "file with an ndk.dir entry in the directory ${gradle_project_root}, or set the " +
                "ANDROID_NDK envrionment variable")
    }

    def ndkBuildExecutable = new File(ext.ndkDir, 'ndk-build')
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
		ndkBuildExecutable= new File(ext.ndkDir, 'ndk-build.cmd')
    }
    if (!ndkBuildExecutable.exists()) {
        throw new GradleException("Could not find ndk-build. The configured NDK directory ${ndkDir} may not be correct.")
    }
	return ndkBuildExecutable;
}

task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
	commandLine(getNDKCommand(), '-j', Runtime.runtime.availableProcessors(), '-C', file('src/main/jni').absolutePath)
}

task cleanNative(type: Exec, description: 'Clean JNI object files') {
	commandLine(getNDKCommand(), '-C', file('src/main/jni').absolutePath, 'clean')
}

clean.dependsOn 'cleanNative'


tasks.withType(JavaCompile) {
   compileTask -> compileTask.dependsOn buildNative
}