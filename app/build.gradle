apply plugin: 'com.android.application'

android {
    compileSdkVersion 23

    defaultConfig {
        applicationId "com.nowsecure.android.vts"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 13
        versionName "v.13"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
        }
    }

    sourceSets.main {
        jni.srcDirs = [] // This prevents the auto generation of Android.mk
        // This tells gradle to include the objects in libs/ which were generated by running our
        // Android.mk basically by shelling out
        jniLibs.srcDir 'src/main/libs'
    }
}

android {
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:23.1.1'
    implementation 'com.android.support:cardview-v7:23.1.1'
    implementation 'com.android.support:design:23.1.1'
    implementation 'com.github.paolorotolo:appintro:3.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta3'
    implementation 'com.squareup.retrofit2:converter-gson:2.0.0-beta3'
    implementation 'com.squareup.retrofit2:retrofit:2.0.0-beta3'
    implementation 'commons-io:commons-io:2.4'
    implementation 'io.reactivex:rxandroid:1.0.1'
    implementation 'org.apache.commons:commons-compress:1.5'
    implementation('com.github.afollestad.material-dialogs:core:0.8.5.3@aar') {
        transitive = true
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def ndkDir = properties.getProperty('ndk.dir')

task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
    commandLine "$ndkDir/ndk-build",
            '-C', file('src/main/jni').absolutePath, // Change src/main/jni the relative path to your jni source
            '-j', Runtime.runtime.availableProcessors()
}

task cleanNative(type: Exec, description: 'Clean JNI object files') {
    commandLine "$ndkDir/ndk-build",
            '-C', file('src/main/jni').absolutePath, // Change src/main/jni the relative path to your jni source
            'clean'
}

clean.dependsOn 'cleanNative'


tasks.withType(JavaCompile) {
   compileTask -> compileTask.dependsOn buildNative
}
